<html>

<head>
<title>Fragment Globe</title>
<meta charset ="utf-8">
<meta http-equiv="X-UA-Compatible" content="chrome=1">  <!-- Use Chrome Frame in IE --> 
</head>
<audio controls autoplay style="position:absolute;right:500px">
  <source src="assets/audio.mp3" type="audio/mpeg">
Your browser does not support the audio element.
</audio>
<body>
<div id="Stats-output">
</div>
<div id="message" style="position:absolute;top:100px"></div> <!-- Pixel offset to avoid FPS counter -->
<canvas id="canvas" style="border: none;" width="1024" height="768" tabindex="1"></canvas>

<script id="vs" type="x-shader/x-vertex">
    precision highp float;

    uniform mat4 u_Model;
    uniform mat4 u_View;
    uniform mat4 u_Persp;
    uniform mat4 u_InvTrans;
	uniform sampler2D u_Bump;
	uniform float u_HeightShadeOnOff;

    attribute vec3 Position;
    attribute vec3 Normal;
    attribute vec2 Texcoord;

    varying vec3 v_Normal;
    varying vec2 v_Texcoord;
    varying vec3 v_Position;
    varying vec3 v_positionMC;

    void main(void)
    {

        v_Normal = (u_InvTrans*vec4(Normal,0.0)).xyz;
        v_Texcoord = Texcoord;
		//vec3 centerToSurface = normalize(
		float height = texture2D(u_Bump, v_Texcoord).r;
        vec4 world = u_Model * (vec4(Position.x, Position.y, Position.z, 1.0) + u_HeightShadeOnOff * height * 0.05 * vec4(Position.x, Position.y, Position.z, 0.0));
        vec4 camera = u_View * world;
        v_Position = camera.xyz;
        //v_positionMC = Position;
		v_positionMC = vec3(Position.x, Position.y, Position.z) + u_HeightShadeOnOff * height * 0.05 * vec3(Position.x, Position.y, Position.z);
        gl_Position = u_Persp * camera;
    }
</script>

<script id="fs" type="x-shader/x-fragment">
    precision highp float;

    //View-Space directional light
    //A unit vector
    uniform vec3 u_CameraSpaceDirLight;

    //Diffuse texture map for the day
    uniform sampler2D u_DayDiffuse;
    //Ambient texture map for the night side
    uniform sampler2D u_Night;
    //Color map for the clouds
    uniform sampler2D u_Cloud;
    //Transparency map for the clouds.  Note that light areas are where clouds are NOT
    //Dark areas are where clouds are present
    uniform sampler2D u_CloudTrans;
    //Mask of which areas of the earth have specularity
    //Oceans are specular, landmasses are not
    uniform sampler2D u_EarthSpec;
    //Bump map
    uniform sampler2D u_Bump;

    uniform float u_time;
    uniform mat4 u_InvTrans;
	uniform float u_BumpOnOff;
	uniform float u_CloudShadowOnOff;
	uniform float u_RimLightOnOff;
	uniform float u_WaterRendering;
	uniform float u_GammaCorrect;
	uniform float u_WindSpeed;

    varying vec3 v_Normal;              // surface normal in camera coordinates
    varying vec2 v_Texcoord;
    varying vec3 v_Position;            // position in camera coordinates
    varying vec3 v_positionMC;          // position in model coordinates

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC);
	float rand(vec2 co);

    void main(void)
    {
		float earthSpec = texture2D(u_EarthSpec, v_Texcoord).r;	
		float center = texture2D(u_Bump, v_Texcoord).r;
		float right = texture2D(u_Bump, v_Texcoord+ vec2(1.0/1024.0, 0.0)).r;
		float top = texture2D(u_Bump, v_Texcoord+ vec2(0.0, 1.0/512.0)).r;
		vec3 perturbeNormal = normalize(vec3(center - right, center - top, 0.2));
		
		mat3 transformMatrix = eastNorthUpToEyeCoordinates(v_positionMC, v_Normal);
		vec3 bumpNormal = transformMatrix * perturbeNormal;
		
        // surface normal - normalized after rasterization
        vec3 normalBumpOffNoiseOff = normalize(v_Normal);

		
		//Compute noise for water rendering
		int a = int(u_time / 0.05);
		int b = int((v_Position.x-0.5) / 0.01) + 2;
		int c= int((v_Position.y-0.5) / 0.01) + 2;
		vec3 noise = normalize(vec3(rand(vec2(a*b, a*c+1)), rand(vec2(a*c+2, a*b+3)), rand(vec2(a*b+4, a*c+5))));

		vec3 normal = normalize(bumpNormal) * u_BumpOnOff + normalize(v_Normal) * (1.0 - u_BumpOnOff);	
		normal = normalize(normal + earthSpec * 0.5 * noise * u_WaterRendering);
        // normalized eye-to-position vector in camera coordinates
        vec3 eyeToPosition = normalize(v_Position);
        
        float diffuse = clamp(dot(u_CameraSpaceDirLight, normal), -1.0, 1.0);
		float diffuseForDayAndNight = clamp(dot(u_CameraSpaceDirLight, normalBumpOffNoiseOff), -1.0, 1.0);

        vec3 toReflectedLight = reflect(-u_CameraSpaceDirLight, normal);
        float specular = max(dot(toReflectedLight, -eyeToPosition), 0.0);
        specular = pow(specular, 20.0);



        vec3 dayColor = texture2D(u_DayDiffuse, v_Texcoord).rgb;
        vec3 nightColor = texture2D(u_Night, v_Texcoord).rgb;

		
        //apply gamma correction to nighttime texture
		float gammaCorrect = u_GammaCorrect/1.2; //gamma correct by 1/1.2
        nightColor = pow(nightColor,vec3(gammaCorrect));




		//Make the cloud mapping move 
		vec2 v_TexcoordOffset = v_Texcoord + vec2(u_time * u_WindSpeed, 0.0);

		//Create the cloud mapping effect
		vec3 cloudColor = texture2D(u_Cloud, v_TexcoordOffset).rgb;
		float cloudTrans = texture2D(u_CloudTrans, v_TexcoordOffset).r;
		vec3 obscureColor = vec3(0.0, 0.0, 0.0);
		
		vec2 v_TexcoordCloudShadow = v_TexcoordOffset + vec2(-u_CameraSpaceDirLight.x*0.007, -u_CameraSpaceDirLight.y*0.007);
		float cloudTransShadow = texture2D(u_CloudTrans, v_TexcoordCloudShadow).r;
		
		//Add cloud shadow effect
		dayColor = (1.0-u_CloudShadowOnOff) * dayColor + u_CloudShadowOnOff * mix(obscureColor, dayColor, cloudTransShadow);
		
		//Add water rendering

		
		//Create the rim lighting effect
		float rimFactor = dot(v_Normal, v_Position) + 1.0;
		vec3 rimColor = clamp(vec3(rimFactor / 4.0, rimFactor / 2.0, rimFactor / 2.0), vec3(0.0, 0.0, 0.0), vec3(1.0, 1.0, 1.0));
		vec3 finalDayColor = mix(0.6 *diffuseForDayAndNight* cloudColor, 0.6 * diffuse * dayColor, cloudTrans) + (0.7 * specular * earthSpec) * dayColor + rimColor * u_RimLightOnOff;
		vec3 finalNightColor = 2.0 * mix(obscureColor, nightColor, cloudTrans) + rimColor * u_RimLightOnOff;
		
		vec3 color;
		if(diffuseForDayAndNight > 0.1)//Day
			color = finalDayColor;
		else if(diffuseForDayAndNight < -0.1)//Night
			color = finalNightColor;
		else
			color = mix(finalNightColor,
						finalDayColor,  			    
						(diffuseForDayAndNight+0.1) /0.2 )  ;

        gl_FragColor = vec4(color, 1.0);
    }

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC)
    {
        // normalized surface tangent in model coordinates
        vec3 tangentMC = normalize(vec3(-positionMC.z, positionMC.x, 0.0)); 
        // normalized surface tangent in eye coordiantes 
        vec3 tangentEC = normalize(mat3(u_InvTrans) * tangentMC);
        // normalized surface bitangent in eye coordinates
        vec3 bitangentEC = normalize(cross(normalEC, tangentEC));

        return mat3(
            tangentEC.x,   tangentEC.y,   tangentEC.z,
            bitangentEC.x, bitangentEC.y, bitangentEC.z,
            normalEC.x,    normalEC.y,    normalEC.z);
    }
	
	float rand(vec2 co){
		return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
	}

</script>


<script src ="js/lib/gl-matrix.js" type ="text/javascript"></script>
<script src ="js/webGLUtility.js" type ="text/javascript"></script>
<script src ="js/frag_globe.js" type ="text/javascript"></script>
<script src="js/lib/dat.gui.min.js" type="text/javascript"></script>
<script src="js/lib/stats.min.js" type="text/javascript" ></script>
</body>

</html>
